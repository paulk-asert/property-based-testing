/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath 'org.nosphere.apache:creadur-rat-gradle:0.7.1'
    }
}

plugins {
    id 'org.kordamp.gradle.project' version '0.47.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

repositories {
    mavenCentral()
}

config {
    info {
        name = 'property-based-testing'
        inceptionYear = '2019'
        vendor = 'groovycookbook.org'
        description = 'Examples of Property-based Testing mostly using Groovy'
        links {
            website      = 'https://github.com/paulk-asert/property-based-testing'
            issueTracker = 'https://github.com/paulk-asert/property-based-testing/issues'
            scm          = 'https://github.com/paulk-asert/property-based-testing.git'
        }

        people {
            person {
                id    = 'paulk-asert'
                name  = 'Paul King'
                roles = ['developer']
            }
        }
    }

    publishing { enabled = false }

    buildInfo {
        skipBuildRevision = true
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }
}

ext.UNSTABLE = /^[0-9,.-]+[.-](alpha|beta|rc|snap|m)[.\d-]*$/
// ignore non-stable releases
allprojects {
    apply plugin: 'org.nosphere.apache.rat'
    tasks.named("dependencyUpdates").configure {
        gradleReleaseChannel = 'current'
        outputFormatter = 'plain'
        rejectVersionIf {
            !(it.currentVersion.toLowerCase() ==~ UNSTABLE) && it.candidate.version.toLowerCase() ==~ UNSTABLE
        }
    }
}

rat {
    inputDir = projectDir
    excludes = ['subprojects/**', '**/.gradle/**', '**/wrapper/**', 'gradlew*', 'gradle.properties', '.idea/**', 'build/**']
}
subprojects { sp ->
    rat {
        inputDir = sp.projectDir
        excludes = [ 'target/**', 'build/**', '.gradle/**', '*.iml', '.classpath', '.project', 'out/**', '.settings/**', 'bin/**']
    }
    config {
        coverage {
            jacoco {
                toolVersion = '0.8.8'
            }
        }
    }
}
